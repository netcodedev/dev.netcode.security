plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'dev.netcode'
version = '0.1.0'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(11)
	}
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId = 'dev.netcode'
            artifactId = 'security'
			version = '0.1.0'
            pom {
                name = 'netcodes Java security Utilities'
                description = 'Security utilities including encryption'
                url = 'https://github.com/netcodedev/dev.netcode.security'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/netcodedev/dev.netcode.security/blob/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'netcode'
                        name = 'Benedict Mihm'
                        email = 'benmihm@netcode.dev'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/netcodedev/dev.netcode.security.git'
                    url = 'https://github.com/netcodedev/dev.netcode.security'
                }
            }

        }
    }
    repositories {
    	maven {
    		name = "OSSRH"
    		credentials {
    			username = System.getenv("MAVEN_USERNAME")
    			password = System.getenv("MAVEN_PASSWORD")
    		}
    		def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
    		def snapshotRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
    		url = version.endsWith('SNAPSHOT') ? snapshotRepoUrl : releasesRepoUrl
    	}
    }
}

artifacts {
    archives javadocJar, sourcesJar
}
signing {
    sign publishing.publications.maven
}
javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}
dependencies {
    // Use JUnit Jupiter for testing.
    testImplementation 'org.junit.jupiter:junit-jupiter:5.12.0'

    // netcode util
    implementation 'dev.netcode:util:0.1.0'

    // JSON
    implementation 'org.json:json:20250107'

    // GSON
    implementation 'com.google.code.gson:gson:2.12.1'

    // LOMBOK
    compileOnly 'org.projectlombok:lombok:1.18.36'
	annotationProcessor 'org.projectlombok:lombok:1.18.36'

	testCompileOnly 'org.projectlombok:lombok:1.18.36'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.36'
}

tasks.named('test') {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform()
}
